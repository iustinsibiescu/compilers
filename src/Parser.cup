import java_cup.runtime.*;
   

parser code {:
    
	boolean syntaxErrors;

	public void syntax_error(Symbol current_token) {
	syntaxErrors = true;
	report_error(
		"Syntax error at line " + (current_token.left+1) + ", column "
		+ current_token.right + current_token.toString() +"\n\n\n\n\n", null
	);
	}
	public void report_fatal_error(String message, Object info) {
		report_error("", null);
		done_parsing();
	}

:};
   

terminal 
	ID,
	BOOLEAN_TYPE,
	CHARACTER_TYPE,
	INTEGER_TYPE,
	RATIONAL_TYPE,
	FLOAT_TYPE,
	DICT_TYPE,
	SEQ_TYPE,
	BOOLEAN_VALUE,
	CHARACTER_VALUE,
	INTEGER_NUMBER,
	FLOAT_NUMBER,
	DICT_VALUE,
	SEQ_VALUE,
	RATIONAL_NUMBER,
	LPAREN,
	RPAREN,
	PLUS,
	MINUS,
	TIMES,
	DIVIDE,
	GT,
	GTE,
	LT,
	LTE,
	EQ,
	NEQ,
	AND,
	OR,
	NOT,
	SEMI,
	COLON,
	ASSIGN,
	IF,
	FI,
	THEN,
	ELSE,
	WHILE,
	DO,
	OD,
	FORALL,
	IN,
	READ,
	PRINT,
	MAIN,
	LBRACKET,
	RBRACKET,
	LCURL,
	RCURL,
	COMMA,
	RETURN,
	MAIN_RETURN,
	FDEF;

non terminal Object
	program,
	function_declr,
	return,
	return_val,
	formal_param_list,
	main,
	body,
	while_block,
	if_block,
	forall_block,
	declrs,
	stmts,
	stmt,
	declr,
	init_declr,
	normal_declr,
	dict_declr,
	dict_value,
	dict_body,
	seq_declr,
	seq_value,
	seq_elements,
	slicing,
	slicing_index,
	assignment,
	type,
	loc,
	array_element,
	in_block,
	iterable,
	function_call,
	param_list,
	bool,
	join,
	equality,
	rel,
	expr,
	term,
	unary,
	factor,
	numerical_val;


	program ::= 
		declrs main declrs
		| declrs main
		| main declrs
		| main
		;

	function_declr ::= 
		FDEF ID LPAREN formal_param_list RPAREN LCURL body return RCURL COLON type
		| FDEF ID LPAREN formal_param_list RPAREN LCURL body RCURL
		;

	return ::= 
		RETURN return_val SEMI
		;

	return_val ::= 
		bool
		;

	formal_param_list ::= 
		normal_declr COMMA formal_param_list
		| normal_declr
		|
		;

	main ::= 
		MAIN LCURL body RCURL SEMI
		;

	body ::= 
		declrs stmts
		| stmts
		;

	while_block ::= 
		WHILE LPAREN bool RPAREN DO body OD
		;

	if_block ::= 
		IF LPAREN bool RPAREN THEN body ELSE body FI
		| IF LPAREN bool RPAREN THEN body FI 
		; 

	forall_block ::=
		FORALL LPAREN in_block RPAREN DO body OD
		;

	declrs ::= 
		declrs declr
		| declr
		
		;

	stmts ::= 
		stmts stmt
		| stmt
		;

	stmt ::= 
		ID assignment SEMI
		| function_call SEMI
		| array_element SEMI
		| if_block
		| while_block
		| forall_block
		| MAIN_RETURN
		| READ ID SEMI
		| READ array_element SEMI
		| PRINT bool SEMI
		;

	

	declr ::= 
		normal_declr SEMI
		| init_declr SEMI
		| dict_declr SEMI
		| seq_declr SEMI
		| function_declr SEMI
		;

	normal_declr ::= 
		ID COLON type
		;

	init_declr ::= 
		normal_declr assignment
		;

	dict_declr ::=
		ID COLON DICT_TYPE ASSIGN dict_value
		| ID COLON DICT_TYPE
		;

	dict_value ::=
		LCURL dict_body RCURL
		;

	dict_body ::=
		loc COLON loc COMMA dict_body
		| loc COLON loc
		|
		;

	seq_declr ::=
		ID COLON SEQ_TYPE ASSIGN seq_value
		| ID COLON SEQ_TYPE
		;

	seq_value ::=
		LBRACKET seq_elements RBRACKET
		| slicing
		;


	seq_elements ::= 
		bool COMMA seq_elements
		| bool
		| LBRACKET seq_elements RBRACKET COMMA seq_elements
		| LBRACKET seq_elements RBRACKET
		| slicing COMMA seq_elements
		| slicing
		|
		;

	slicing ::=
		ID LBRACKET slicing_index COLON slicing_index RBRACKET 
		| ID LBRACKET COLON slicing_index RBRACKET 
		| ID LBRACKET slicing_index COLON RBRACKET
		;

	slicing_index ::=
		ID
		| INTEGER_NUMBER
		| function_call
		;


	assignment ::= 
		ASSIGN bool
		;

	type ::= 
		INTEGER_TYPE
		| FLOAT_TYPE
		| RATIONAL_TYPE
		| BOOLEAN_TYPE
		| CHARACTER_TYPE
		;

	loc ::=
		ID
		| numerical_val
		| BOOLEAN_VALUE
		
		| CHARACTER_VALUE
		| DICT_VALUE
		| SEQ_VALUE
		| function_call
		| array_element 
		;

	array_element ::=
		ID LBRACKET loc RBRACKET
		;
	

	in_block ::= 
		bool IN iterable
		;

	iterable ::=
		dict_value
		| seq_value
		| array_element
		| ID
		;
				
	
	function_call ::=
		ID LPAREN param_list RPAREN	
		;

	param_list ::=
		bool COMMA param_list
		| bool
		;

	bool ::= 
		bool OR join 
		| join
		| in_block
		;

	join ::= 
		join AND equality
		| equality
		;

	equality ::=
		equality EQ rel
		| equality NEQ rel
		| rel
		;

	rel ::=
		expr LT expr
		| expr LTE expr
		| expr GTE expr
		| expr GT expr
		| expr
		;

	expr ::=
		expr PLUS term
		| expr MINUS term
		| term
		;
	
	term ::=
		term TIMES unary
		| term DIVIDE unary
		| unary
		;

	unary ::=
		NOT unary
		| MINUS unary
		| factor
		;

	factor ::=
		LPAREN bool RPAREN
		| loc
		;
		
	numerical_val ::= 
		INTEGER_NUMBER
		| FLOAT_NUMBER
		| RATIONAL_NUMBER
		;
   
   
